ext {
    versionMajor = 0
    versionMinor = 0
    versionPatch = 0

    compileSdk = 28
    minSdk = 21
    targetSdk = 28

    versionClassifier = null
    isSnapshot = true

    appVersionCode = generateVersionCode(
            minSdk,
            versionMajor,
            versionMinor,
            versionPatch
    )

    appVersionName = generateVersionName(
            versionMajor,
            versionMinor,
            versionPatch,
            versionClassifier,
            isSnapshot
    )
}

ext.versions = [
        'kotlin' : '1.3.31',
        'appcompat' : '1.1.0-alpha05',
        'coreKtx' : '1.0.2',
        'constraintlayout' : '1.1.3',
        'junit' : '4.12',
        'runner' : '1.2.0',
        'espressoCore' : '3.2.0',
        'materialDesign' : '1.1.0-alpha06'
]

private static Integer generateVersionCode(Integer minSdk, Integer versionMajor, Integer versionMinor, Integer versionPatch) {
    return minSdk * 10000000 + versionMajor * 10000 + versionMinor * 100 + versionPatch
}

private static String generateVersionName(Integer versionMajor, Integer versionMinor, Integer versionPatch, String versionClassifier, Boolean isSnapshot) {
    String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    if (versionClassifier == null && isSnapshot) {
        versionClassifier = "SNAPSHOT"
    }

    if (versionClassifier != null) {
        versionName += "-" + versionClassifier
    }
    return versionName
}
